plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.junioraww'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "elytrium-repo"
        url = "https://maven.elytrium.net/repo/"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:3.4.0-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity-proxy:3.4.0-SNAPSHOT"
    compileOnly "libs:limboapi-1.1.27-SNAPSHOT"
    compileOnly "net.kyori:adventure-nbt:4.12.0"
    implementation 'de.mkammerer:argon2-jvm:2.12'
    annotationProcessor "com.velocitypowered:velocity-api:3.4.0-SNAPSHOT"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 21 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

/*tasks.register('moveJar', Copy) {
    dependsOn build
    from("build/libs")
    into("/home/fox/desktop/verTest/vel/plugins")
    include("AuthVee-1.0.jar")
}*/

// Привязываем shadowJar к обычному build
build {
    dependsOn shadowJar
}

shadowJar {
    archiveBaseName.set("AuthVee")
    archiveClassifier.set("") // чтобы был обычный JAR
    archiveVersion.set("1.0")

    exclude "com/sun/jna/aix-ppc/**"
    exclude "com/sun/jna/aix-ppc64/**"
    exclude "com/sun/jna/darwin-aarch64/**"
    exclude "com/sun/jna/darwin-x86-64/**"
    exclude "com/sun/jna/dragonflybsd-x86-64/**"
    exclude "com/sun/jna/freebsd-aarch64/**"
    exclude "com/sun/jna/freebsd-x86*/**"
    exclude "com/sun/jna/linux-aarch64/**"
    exclude "com/sun/jna/linux-arm/**"
    exclude "com/sun/jna/linux-armel/**"
    exclude "com/sun/jna/linux-loongarch64/**"
    exclude "com/sun/jna/linux-mips64el/**"
    exclude "com/sun/jna/linux-ppc/**"
    exclude "com/sun/jna/linux-ppc64le/**"
    exclude "com/sun/jna/linux-riscv64/**"
    exclude "com/sun/jna/linux-s390x/**"
    exclude "com/sun/jna/linux-x86/**"
    exclude "com/sun/jna/openbsd-x86/**"
    exclude "com/sun/jna/openbsd-x86-64/**"
    exclude "com/sun/jna/sunos-sparc/**"
    exclude "com/sun/jna/sunos-sparcv9/**"
    exclude "com/sun/jna/sunos-x86/**"
    exclude "com/sun/jna/sunos-x86-64/**"
    exclude "com/sun/jna/win32/**"
    exclude "com/sun/jna/win32-aarch64/**"
    exclude "com/sun/jna/win32-x86/**"
    exclude "com/sun/jna/win32-x86-64/**"
    //exclude "**/libjnidispatch.*"

    mergeServiceFiles()
}

//build.finalizedBy(moveJar)